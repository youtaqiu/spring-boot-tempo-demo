buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.spring.io/plugins-release" }
    }

    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
        classpath "gradle.plugin.com.google.cloud.tools:jib-gradle-plugin:3.1.4"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.5.4"
    }

}

apply plugin: "io.spring.dependency-management"

subprojects {
    apply plugin: "java-library"

    sourceCompatibility = "$javaVersion"
    targetCompatibility = "$javaVersion"
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    repositories {
        mavenLocal()
        maven { url "https://maven.aliyun.com/repository/public" }
        maven { url "https://maven.aliyun.com/repository/spring" }
        maven { url "https://maven.aliyun.com/repository/spring-plugin" }
        maven { url 'https://repo.spring.io/release' }
        mavenCentral()
    }

    dependencies {
        implementation platform("me.youm.frame:shore-bom:${project.shoreVersion}")
        compileOnly "org.projectlombok:lombok:${project.lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${project.lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${project.lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${project.lombokVersion}"
        testCompileOnly "org.springframework.boot:spring-boot-starter-test"
        testImplementation "junit:junit"
    }

    compileJava.dependsOn(processResources)


    apply plugin: "com.google.cloud.tools.jib"

    jib {
        from {
            image = project.baseDockerImage
        }
        to {
            image = "youtaqiu/${project.name}"
            tags = ["latest"]
        }
        container {
            creationTime = 'USE_CURRENT_TIMESTAMP'
            jvmFlags = ['-Djava.security.egd=file:/dev/./urandom', '-Dfile.encoding=utf-8', '-Duser.timezone=GMT+08','-javaagent:/opentelemetry.jar']
        }
    }
}